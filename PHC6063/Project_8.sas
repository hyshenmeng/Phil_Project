/* INPUT DATASET */
PROC IMPORT DATAFILE= "F:\PJ8a.csv"
  OUT=PJ8a 
  DBMS=CSV REPLACE;
  GETNAMES=YES;    
RUN;

/* SUMMARIZE CATEGORICAL VARIABLES */
PROC FREQ DATA=PJ8a;
  BY Intervention;
  TABLE Successful_Removal Type_of_Model Location Complications;
RUN;

/* SELECT UNSUCCESSFUL OBSERVATIONS */
DATA PJ8a_US;
  SET PJ8a;
  IF Successful_Removal=1 THEN DELETE;
RUN;
PROC PRINT DATA=PJ8a_US;
RUN;

/* MACRO FOR FISHER'S EXACT TEST */
%MACRO FISHERTEST(x,y);
PROC FREQ DATA=PJ8a;
  TABLES &x*&y;
  EXACT FISHER;
RUN;
%MEND;

/* ANALYSIS OF SUCCESSFUL_REMOVAL*INTERVENTION */
%FISHERTEST(Successful_Removal,Intervention);

/* ANALYSIS OF SUCCESSFUL_REMOVAL*TYPE_OF_MODEL */
%FISHERTEST(Successful_Removal,Type_of_Model);

/* ANALYSIS OF SUCCESSFUL_REMOVAL*LOCATION */
%FISHERTEST(Successful_Removal,Location);

/* ANALYSIS OF SUCCESSFUL_REMOVAL*COMPLICATIONS */
%FISHERTEST(Successful_Removal,Complications);

/* ANALYSIS OF TYPE_OF_MODEL*INTERVENTION */
%FISHERTEST(Type_of_Model,Intervention);

/* ANALYSIS OF LOCATION*INTERVENTION */
%FISHERTEST(Location,Intervention);

/* ANALYSIS OF COMPLICATIONS*INTERVENTION */
%FISHERTEST(Complications,Intervention);

/* SELECT SUCCESSFUL OBSERVATIONS */
DATA PJ8a_S;
  SET PJ8a;
  IF Successful_Removal=2 THEN DELETE;
  Size=(CONSTANT('PI')/4)*Size_1*Size_2;
RUN;

/* SUMMARIZE CONTINUOUS VARIABLES */
PROC MEANS DATA=PJ8a_S MEAN MEDIAN MIN MAX RANGE VAR STD;
  BY Intervention;
  VAR Removal_Time Size;
RUN;

/* MACRO FOR KRUSKAL-WALLIS RANK SUM TEST */
%MACRO RANKSUMTEST(x,y);
PROC NPAR1WAY DATA=PJ8a_S;
  CLASS &x;
  VAR &y;
RUN;
%MEND;

/* KRUSKAL-WALLIS RANK SUM TEST FOR INTERVENTION WRT REMOVAL_TIME */
%RANKSUMTEST(Intervention,Removal_Time);

/* KRUSKAL-WALLIS RANK SUM TEST FOR MODEL WRT REMOVAL_TIME */
%RANKSUMTEST(Type_of_Model,Removal_Time);

/* KRUSKAL-WALLIS TEST FOR LOCATION WRT REMOVAL_TIME */
%RANKSUMTEST(Location,Removal_Time);

/* KRUSKAL-WALLIS TEST FOR COMPLICATIONS WRT REMOVAL_TIME */
%RANKSUMTEST(Complications,Removal_Time);

/* KRUSKAL-WALLIS TEST FOR INTERVENTION WRT SIZE */
%RANKSUMTEST(Intervention,Size);

/* KRUSKAL-WALLIS TEST FOR MODEL WRT SIZE */
%RANKSUMTEST(Type_of_Model,Size);

/* KRUSKAL-WALLIS TEST FOR LOCATION WRT SIZE */
%RANKSUMTEST(Location,Size);

/* KRUSKAL-WALLIS TEST FOR COMPLICATIONS WRT SIZE */
%RANKSUMTEST(Complications,Size);

/* FIT LINEAR REGRESSION */
PROC REG DATA=PJ8a_S;
  MODEL Removal_Time=Lesion__;
RUN; 

/* FIT LINEAR REGRESSION */
PROC REG DATA=PJ8a_S;
  MODEL Removal_Time=Lesion__ Intervention Size;
RUN; 

/* INPUT DATASET */
PROC IMPORT DATAFILE= "F:\PJ8b.csv"
  OUT=PJ8b 
  DBMS=CSV REPLACE;
  GETNAMES=YES;    
RUN;

/* SELECT SUCCESSFUL OBSERVATIONS */
DATA PJ8b_S;
  SET PJ8b;
  IF Successful_Removal=2 THEN DELETE;
  Size=(CONSTANT('PI')/4)*Size_1*Size_2;
RUN;

/* FIT LINEAR REGRESSION */
PROC REG DATA=PJ8b_S;
  MODEL Removal_Time=Lesion__ Intervention Size;
RUN;








